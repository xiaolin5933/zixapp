#!perl
use feature qw/state/;
use ZAPP::BIP::Constant;

#
# $svc对象:  $cfg
#-----------------------------------------
# req:  {
#   data => {
#       _type           => "0009",
#       bi              => 1,
#       c               => "cid",
#       cust_proto      => 1,
#       memo            => "",
#       ssn             => "ssn123",
#       tx_amt          => 244,
#       wk_cfee         => 123,
#       period          => "2013-04-02",
#       zjbd_date_out   => "2013-04-02",
#   },
#   svc  => "yspz_0009",
#   sys  => { oper_user => 1 },
# }
#
# res:  {
#     status => 0,  # 0 成功, 其他失败
#     errmsg => '',
#     ret    => ''
# }
#
#
sub {
    my ($svc, $req) = @_;

    my $ret = {
        status => -1,
        errmsg => '',
        ret    => ''
    };

    my $src = $req->{data};

    state $sth = $svc->{dbh}->prepare("select dept_bi from dept_bi where dept_id = ? and bi = ?");
    $sth->execute(51, $src->{bi});
    my ($dept_bi) = $sth->fetchrow_array() or return $ret;

    # 获取10项输出数组第一个数组
    my $config  = $svc->{bip};
    my $inst = $config->inst(51, $dept_bi);
    my $res = $inst->calc( { amt  => $src->{tx_amt}, date => $src->{zjbd_date_out} })->[0];

    my $c = $src->{c};
    # 本金备付金帐号，通过银行接口中的银行协议获取
    $src->{bfj_acct_bj} = $res->[RES_BJ_ACCT];
    $src->{period}      = $src->{zjbd_date_out};
    $src->{p}           = 3;
    $src->{tx_date}     = $src->{zjbd_date_out};
    $src->{cwwf_bfee}   = $res->[RES_BFEE_CWWF] || 0; 
    $src->{status}      = '0';
    $src->{c}           = '51.' . $c;
    $src->{cust_proto}  = '3_'  . $c;

    # 插入原始凭证
    $src->{id} = $svc->{zark}->yspz_ins( 
        $src->{_type}, 
        @{$src}{@{$svc->{zark}->yspz_flist($src->{_type})}} 
    );
    $svc->{dbh}->commit;

    # send
    $svc->{_stomp}->send({ 
        destination => $svc->{stomp}->{queue}->{proc}, 
        body        => $svc->{serializer}->serialize($src)
    });

    $ret->{status} = 0;
    
    return $ret;
};

